function healthSystemHealing(p: player, name: text, time: int):

    #CHECK IF HEALTH AND TOOL ARE IN PLACE
    {_p}'s health is not max health of {_p}
    name of {_p}'s tool = {_name}

    #CHECK IF PLAYER IS ALREADY USING ANOTHER HEAL
    (metadata tag "Heal_Active" of {_p}) ? false = false
    set (metadata tag "Heal_Active" of {_p}) to true

    #REMOVE ITEM FROM INVENTORY
    set {_i} to {_p}'s tool
    remove 1 of {_i} from {_p}'s inventory

    #HEALING EVENT
    loop {_time} times:
        exit loop if {_p}'s health >= 10
        send action bar "&dApplying %font({_name})%&7... &8[&c%textB({_t},{_time}-1,"▉▉▉▉▉▉▉▉▉▉","&a","&7")%&8]" to {_p}
        add 1 to health of {_p}
        add 1 to {_t}
        wait a second
    
    #ALLOW PLAYER TO USE ANOTHER HEAL
    send action bar "&aSuccessfully Healed" to {_p}
    set (metadata tag "Heal_Active" of {_p}) to false

    healthSystemCooldown({_p}, {_i}, 3 seconds) if {_name} = "&7Medkit"



function healthSystemCooldown(p: player, item: item, time: timespan):

    item cooldown of {_item} for {_p} = 0 seconds
    set item cooldown of {_item} for {_p} to {_time}

on consume:

    event-item = golden apple
    set (metadata tag "Heal_Active" of player) to true
    healthSystemCooldown(player, event-item, 4 seconds)
    wait 4 seconds
    set (metadata tag "Heal_Active" of player) to false

on right click:
    healthSystemHealing(player, "&7Bandage", 2)
    healthSystemHealing(player, "&7Medkit", 10)