# SCP-914 Script | Made by Almosst or https://github.com/Almosst-DEV
# Only authorized for use by Dangerzone.
# For any issues/complaints please reach out to myself via Discord (almosst.)

# Functions

local function SCP914_ItemCreate(p:player,i:item):
	add 1 to {914items::amount}
	set {_id} to {914items::amount}
	set {914items::items::%{_id}%} to 1 of {_i}
	set {_name} to name of {_i}
	set {_itemtype} to "%{_i}'s type%"
	set {_defaultchance} to "100"
	set {_id2} to "%{_id}%"
	load yaml "plugins/Skript/yaml/scp914items/item-%{_id}%" as "item-%{_id}%"
	SCP914_YMLValue({_id},"name",{_name})
	SCP914_YMLValue({_id},"itemtype",{_itemtype})
	SCP914_YMLValue({_id},"creator",uuid of {_p})
	SCP914_YMLValue({_id},"upgrades.1.item1",{_id2})
	SCP914_YMLValue({_id},"upgrades.1.item2","1")
	SCP914_YMLValue({_id},"upgrades.1.chance",{_defaultchance})
	SCP914_YMLValue({_id},"upgrades.2.item1",{_id2})
	SCP914_YMLValue({_id},"upgrades.2.item2","1")
	SCP914_YMLValue({_id},"upgrades.2.chance",{_defaultchance})
	SCP914_YMLValue({_id},"upgrades.3.item1",{_id2})
	SCP914_YMLValue({_id},"upgrades.3.item2","1")
	SCP914_YMLValue({_id},"upgrades.3.chance",{_defaultchance})
	SCP914_YMLValue({_id},"upgrades.4.item1",{_id2})
	SCP914_YMLValue({_id},"upgrades.4.item2","1")
	SCP914_YMLValue({_id},"upgrades.4.chance",{_defaultchance})
	SCP914_YMLValue({_id},"upgrades.5.item1",{_id2})
	SCP914_YMLValue({_id},"upgrades.5.item2","1")
	SCP914_YMLValue({_id},"upgrades.5.chance",{_defaultchance})
	save yaml "item-%{_id}%"
    
local function SCP914_YMLValue(id:int,dataid:string,data:string):
    set yaml value "%{_dataid}%" from "item-%{_id}%" to "%{_data}%"
    
local function SCP914_YMLGetValue(id:int,dataid:string) :: string:
	if yaml value "%{_dataid}%" from "item-%{_id}%" exists:
		set {_r} to yaml value "%{_dataid}%" from "item-%{_id}%"
		return "%{_r}%"
	return "&cData not found!"
	
local function SCP914_ItemConfig(p:player,id:int):
	if {_p} is op:
		delete (metadata tag "914item_select_id" of {_p})
		create a gui with virtual chest inventory with 3 rows named "&1SCP-914 &8| &cᴀᴅᴍɪɴ ᴍᴇɴᴜ":
			loop (9*3) times:
				format gui slot (loop-number - 1) with black stained glass pane named "&7"
			format gui slot 0 with barrier named "&cBack" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ɢᴏ ʙᴀᴄᴋ":
				SCP914_ItemMenu({_p})
			format gui slot 10 with {914items::items::%{_id}%}:
				SCP914_AddItem({_p},true,{_id},{_emptyitem})
			format gui slot 3 with red concrete named "&cRough" with lore "&7ѕᴇʟᴇᴄᴛ ɪᴛᴇᴍ(ѕ) ʙᴇʟᴏᴡ"
			format gui slot 4 with orange concrete named "&6Coarse" with lore "&7ѕᴇʟᴇᴄᴛ ɪᴛᴇᴍ(ѕ) ʙᴇʟᴏᴡ"
			format gui slot 5 with yellow concrete named "&e1:1" with lore "&7ѕᴇʟᴇᴄᴛ ɪᴛᴇᴍ(ѕ) ʙᴇʟᴏᴡ"
			format gui slot 6 with lime concrete named "&aFine" with lore "&7ѕᴇʟᴇᴄᴛ ɪᴛᴇᴍ(ѕ) ʙᴇʟᴏᴡ"
			format gui slot 7 with light blue concrete named "&bVery Fine" with lore "&7ѕᴇʟᴇᴄᴛ ɪᴛᴇᴍ(ѕ) ʙᴇʟᴏᴡ"
			set {_gui1} to 12
			set {_gui2} to 21
			loop 5 times:
				set {_upgradevalue} to loop-number
				set {_upgrade1.%{_upgradevalue}%} to SCP914_YMLGetValue({_id},"upgrades.%{_upgradevalue}%.item1")
				set {_upgrade2.%{_upgradevalue}%} to SCP914_YMLGetValue({_id},"upgrades.%{_upgradevalue}%.item2")
				set {_chance.%{_upgradevalue}%} to SCP914_YMLGetValue({_id},"upgrades.%{_upgradevalue}%.chance")
				set {_name} to SCP914_YMLGetValue({_id},"name")
				format gui slot {_gui1} with {914items::items::%{_upgrade1.%{_upgradevalue}%}%} with lore "&fᴄʜᴀɴᴄᴇ: %{_chance.%{_upgradevalue}%}%%%%nl%&7ᴄʟɪᴄᴋ ᴛᴏ ʀᴇᴘʟᴀᴄᴇ ᴄᴜʀʀᴇɴᴛ ɪᴛᴇᴍ":
					SCP914_SelectMenu({_p},{_id},{_upgradevalue},1)
				if {_chance.%{_upgradevalue}%} is not "100":
					if {_upgrade2.%{_upgradevalue}%} is not "<none>":
						format gui slot {_gui2} with {914items::items::%{_upgrade2.%{_upgradevalue}%}%} with lore "&eɪᴛᴇᴍ ɪѕ ᴘɪᴄᴋᴇᴅ ɪꜰ ᴛʜᴇ ᴀʙᴏᴠᴇ ɪᴛᴇᴍ ɪѕ ɴᴏᴛ%nl%&7ᴄʟɪᴄᴋ ᴛᴏ ʀᴇᴘʟᴀᴄᴇ ᴄᴜʀʀᴇɴᴛ ᴏᴠᴇʀꜰʟᴏᴡ ɪᴛᴇᴍ":
							SCP914_SelectMenu({_p},{_id},{_upgradevalue},2)
					else:
						format gui slot {_gui2} with barrier named "&c&lDatabase Error &8| &fID: %{_id}%" with lore "&7ᴘʟᴇᴀѕᴇ ᴄᴏɴᴛᴀᴄᴛ ᴀʟᴍᴏѕѕᴛ ɪꜰ ʏᴏᴜ ѕᴇᴇ ᴛʜɪѕ"
				else:
					format gui slot {_gui2} with red stained glass pane named "&cChance is 100%%, overflow item unnecesary" with lore "&7ᴏᴠᴇʀꜰʟᴏᴡ ɪᴛᴇᴍ ᴡɪʟʟ ɴᴏᴛ ʙᴇ ᴘɪᴄᴋᴇᴅ ɪꜰ ᴄʜᴀɴᴄᴇ ɪѕ 100%%"
				set {_gui1} to ({_gui1} + 1)
				set {_gui2} to ({_gui2} + 1)
		open last gui to {_p}

# Function used to select a new item
local function SCP914_SelectMenu(p:player,oid:int,u:int,type:int):
	if {_p} is op:
		create a gui with virtual chest inventory with 6 rows named "&1SCP-914 &8| &cᴀᴅᴍɪɴ ᴍᴇɴᴜ":
			loop (9*6) times:
				format gui slot (loop-number - 1) with black stained glass pane named "&7"
			if {_type} is 1:
				format gui slot 0 with recovery compass named "&bAdjust Chance" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴀᴅᴊᴜsᴛ ᴘɪᴄᴋ ᴘᴇʀᴄᴇɴᴛᴀɢᴇ":
					set (metadata tag "914chance" of {_p}) to true
					set (metadata tag "914chanceid" of {_p}) to {_oid}
					set (metadata tag "914chanceup" of {_p}) to {_u}
					close {_p}'s inventory
					send "%nl%&fPlease type the percentage you would like as a number from 1-100.%nl%"
			if {914items::amount} >= 36:
				set {_amount} to 36
			else:
				set {_amount} to {914items::amount}
			loop {_amount} times:
				set {_id} to loop-number
				set {_i} to SCP914_YMLGetValue(loop-number,"itemtype")
				set {_n} to SCP914_YMLGetValue(loop-number,"name")
				if {_n} is not "<none>":
					format gui slot (loop-number + 8) with {_i} parsed as item with itemflag hide attributes named {_n} with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴄᴏɴꜰɪɢᴜʀᴇ ᴛʀᴀɴѕꜰᴏʀᴍᴀᴛɪᴏɴѕ":
						SCP914_YMLValue({_oid},"upgrades.%{_u}%.item%{_type}%","%{_id}%")
						save yaml "item-%{_oid}%"
						SCP914_ItemConfig({_p}, {_oid})
				else:
					format gui slot (loop-number + 8) with {_i} parsed as item with itemflag hide attributes with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴄᴏɴꜰɪɢᴜʀᴇ ᴛʀᴀɴѕꜰᴏʀᴍᴀᴛɪᴏɴѕ":
						SCP914_YMLValue({_oid},"upgrades.%{_u}%.item%{_type}%","%{_id}%")
						save yaml "item-%{_oid}%"
						SCP914_ItemConfig({_p}, {_oid})
		open last gui to {_p}

# Function used to select the item you would like to edit
local function SCP914_ItemMenu(p:player):
	if {_p} is op:
		create a gui with virtual chest inventory with 6 rows named "&1SCP-914 &8| &cᴀᴅᴍɪɴ ᴍᴇɴᴜ":
			loop (9*6) times:
				format gui slot (loop-number - 1) with black stained glass pane named "&7"
			format gui slot 0 with barrier named "&cBack" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ɢᴏ ʙᴀᴄᴋ":
				SCP914_AdminMenu(player)
			format gui slot 1 with netherite upgrade smithing template with itemflag hide potion effects named "&aAdd item" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴀᴅᴅ ᴀɴ ɪᴛᴇᴍ ᴛᴏ ᴛʜᴇ ᴄᴏɴꜰɪɢ":
				SCP914_AddItem({_p},false,{_emptyint},{_emptyitem})
			if {914items::amount} >= 36:
				set {_amount} to 36
			else:
				set {_amount} to {914items::amount}
			loop {_amount} times:
				set {_id} to loop-number
				set {_i} to SCP914_YMLGetValue(loop-number,"itemtype")
				set {_n} to SCP914_YMLGetValue(loop-number,"name")
				if {_n} is not "<none>":
					format gui slot (loop-number + 8) with {_i} parsed as item with itemflag hide attributes named {_n} with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴄᴏɴꜰɪɢᴜʀᴇ ᴛʀᴀɴѕꜰᴏʀᴍᴀᴛɪᴏɴѕ":
						SCP914_ItemConfig({_p},{_id})
				else:
					format gui slot (loop-number + 8) with {_i} parsed as item with itemflag hide attributes with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴄᴏɴꜰɪɢᴜʀᴇ ᴛʀᴀɴѕꜰᴏʀᴍᴀᴛɪᴏɴѕ":
						SCP914_ItemConfig({_p},{_id})
		open last gui to {_p}
	
local function SCP914_AddItem(p:player,b:boolean,id:int,i:item):
	if {_i} is not set:
		wait 1 tick
		create a gui with virtual chest inventory with 3 rows named "&1SCP-914 &8| &cᴀᴅᴍɪɴ ᴍᴇɴᴜ":
			loop (9*3) times:
				format gui slot (loop-number - 1) with black stained glass pane named "&7"
			make gui slot 13 with lime stained glass pane named "&aPlease select an item" with lore "&7ᴘʟᴇᴀѕᴇ ᴄʟɪᴄᴋ ʏᴏᴜʀ ɪᴛᴇᴍ ʙᴇꜰᴏʀᴇ ᴄᴏɴᴛɪɴᴜɪɴɢ"
		open the last gui for {_p}
		wait 1 tick
		set (metadata tag "914item_select" of {_p}) to true
		if {_id} exists:
			set (metadata tag "914item_select_id" of {_p}) to {_id}
	else:
		delete (metadata tag "914item_select" of {_p})
		delete (metadata tag "914item_select_id" of {_p})
		create a gui with virtual chest inventory with 3 rows named "&1SCP-914 &8| &cᴀᴅᴍɪɴ ᴍᴇɴᴜ":
			loop (9*3) times:
				format gui slot (loop-number - 1) with black stained glass pane named "&7"
			make gui slot 11 with red stained glass pane named "&cCancel" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ɢᴏ ʙᴀᴄᴋ":
				SCP914_ItemMenu({_p})
			make gui slot 13 with {_i}
			make gui slot 15 with lime stained glass pane named "&aConfirm" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴀᴅᴅ ɪᴛᴇᴍ":
				if {_id} exists:
					set {914items::items::%{_id}%} to {_i}
					set {_name} to name of {_i}
					set {_itemtype} to "%{_i}'s type%"
					SCP914_YMLValue({_id},"name",{_name})
					SCP914_YMLValue({_id},"itemtype",{_itemtype})
					save yaml "item-%{_id}%"
					SCP914_ItemConfig({_p},{_id})
				else:
					SCP914_ItemCreate({_p},{_i})
					wait 1 tick
					SCP914_ItemConfig({_p},{914items::amount})
		open the last gui for {_p}

local function SCP914_AdminMenu(p:player):
	if {_p} is op:
		create a gui with virtual chest inventory with 3 rows named "&1SCP-914 &8| &cᴀᴅᴍɪɴ ᴍᴇɴᴜ":
			loop (9*3) times:
				format gui slot (loop-number - 1) with black stained glass pane named "&7"
			make gui slot 11 with netherite armor smithing template with itemflag hide potion effects named "&aConfigure Item Transformations" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴄᴏɴꜰɪɢᴜʀᴇ ᴛʜᴇ ɪᴛᴇᴍ ᴅᴀᴛᴀʙᴀѕᴇ":
				SCP914_ItemMenu({_p})
			make gui slot 13 with blaze spawn egg named "&eConfigure Entity Transformations" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴄᴏɴꜰɪɢᴜʀᴇ ᴛʜᴇ ᴇɴᴛɪᴛʏ ᴅᴀᴛᴀʙᴀѕᴇ"
			make gui slot 15 with comparator named "&cConfigure SCP-914 Settings" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴄᴏɴꜰɪɢᴜʀᴇ ᴛʜᴇ ѕᴄᴘ"
		open the last gui for {_p}
	else:
		kick {_p}

local function SCP914_Menu(p:player):
	create a gui with virtual chest inventory with 3 rows named "&1SCP-914 &8|":
		loop (9*3) times:
			format gui slot (loop-number - 1) with black stained glass pane named "&7"
		format gui slot 10 with ender eye named "&aActivate SCP-914" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ᴀᴄᴛɪᴠᴀᴛᴇ":
			SCP914_Activate()
			close {_p}'s inventory
		format gui slot 12 with red concrete named "&cRough" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ѕᴀᴠᴇ ѕᴇᴛᴛɪɴɢ":
			set {scp914::setting} to 1
			set line 3 of {scp914::sign} to "&cRough"
			close {_p}'s inventory
			play sound "block.iron_trapdoor.open" with volume 10 and pitch 0 to {_p}
		format gui slot 13 with orange concrete named "&6Coarse" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ѕᴀᴠᴇ ѕᴇᴛᴛɪɴɢ":
			set {scp914::setting} to 2
			set line 3 of {scp914::sign} to "&6Coarse"
			close {_p}'s inventory
			play sound "block.iron_trapdoor.open" with volume 10 and pitch 0 to {_p}
		format gui slot 14 with yellow concrete named "&e1:1" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ѕᴀᴠᴇ ѕᴇᴛᴛɪɴɢ":
			set {scp914::setting} to 3
			set line 3 of {scp914::sign} to "&e1:1"
			close {_p}'s inventory
			play sound "block.iron_trapdoor.open" with volume 10 and pitch 0 to {_p}
		format gui slot 15 with lime concrete named "&aFine" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ѕᴀᴠᴇ ѕᴇᴛᴛɪɴɢ":
			set {scp914::setting} to 4
			set line 3 of {scp914::sign} to "&aFine"
			close {_p}'s inventory
			play sound "block.iron_trapdoor.open" with volume 10 and pitch 0 to {_p}
		format gui slot 16 with light blue concrete named "&bVery Fine" with lore "&7ᴄʟɪᴄᴋ ᴛᴏ ѕᴀᴠᴇ ѕᴇᴛᴛɪɴɢ":
			set {scp914::setting} to 5
			play sound "block.iron_trapdoor.open" with volume 10 and pitch 0 to {_p}
			set line 3 of {scp914::sign} to "&bVery Fine"
			close {_p}'s inventory
	open the last gui for {_p}
	
function SCP914_GetItem(i:item) :: item:
	loop {914items::items::*}:
		if loop-value is {_i}:
			set {_id} to loop-index parsed as int
	set {_i2::item1} to SCP914_YMLGetValue({_id},"upgrades.%{scp914::setting}%.item1")
	set {_i2::chance} to SCP914_YMLGetValue({_id},"upgrades.%{scp914::setting}%.chance")
	if {_i2::chance} parsed as int is not 100:
		set {_i2::item2} to SCP914_YMLGetValue({_id},"upgrades.%{scp914::setting}%.item2")
	chance of {_i2::chance} parsed as int%:
		return {914items::items::%{_i2::item1}%}
	else:
		return {914items::items::%{_i2::item2}%}
	
	
function SCP914_Activate():
	set {_loc} to location of {scp914::sign}
	play sound "item.axe.scrape" with pitch 0 at {_loc}
	wait 2 seconds
	play sound "block.iron_door.close" with pitch 0.6 at {_loc}
	# CLOSE DOORS
	close {scp914::doors::*}
	wait 1.5 seconds
	play sound "entity.minecart.inside" with pitch 0 at {_loc}
	loop 23 times:
		chance of 50%:
			play sound "block.iron_door.close" with pitch 0 at {_loc}
		else:
			play sound "block.iron_door.open" with pitch 0 at {_loc}
		wait 0.5 seconds
	loop all players:
		if "%regions at loop-player%" contains "914-1":
			if {914items::items::*} contains 1 of loop-player's tool:
				set {_item} to SCP914_GetItem(1 of loop-player's tool)
				set loop-player's tool's item amount to loop-player's tool's item amount - 1
				give {_item} to loop-player
			teleport loop-player to location 9 blocks west of loop-player's location
	loop all item entities:
		if "%regions at loop-entity%" contains "914-1":
			if {914items::items::*} contains 1 of loop-entity's item:
				set {_item} to SCP914_GetItem(1 of loop-entity's item)
				set loop-entity's item to {_item}
				teleport loop-entity to location 9 blocks west of loop-entity's location
	play sound "block.fire.extinguish" with pitch 0 at {_loc}
	wait 2 seconds
	play sound "block.note_block.chime" at {_loc}
	wait 0.5 seconds
	play sound "block.iron_door.open" with pitch 0.6 at {_loc}
	open {scp914::doors::*}
				
# Commands

command /914activate:
	permission: op
	trigger:
		SCP914_Activate()

command /914adminmenu:
	permission: op
	permission message: "&c&lSYSTEM &7» &fDid not recognize command."
	trigger:
		if player is op:
			SCP914_AdminMenu(player)
			
command /914setbutton:
	permission: scp914.admin
	trigger:
		set {_nbt} to nbt compound of player's target block
		set int tag "Is914button" of {_nbt} to 1

# Events

on inventory click:
	if (metadata tag "914item_select" of player) is true:
		if (metadata tag "914item_select_id" of player) exists:
			SCP914_AddItem(player,true,(metadata tag "914item_select_id" of player),1 of event-item)
		else:
			SCP914_AddItem(player,false,{_emptyint},1 of event-item)
	
on right click:
	set {_nbt} to nbt compound of event-block
	if int tag "Is914button" of {_nbt} is 1:
		SCP914_Menu(player)
		
on inventory close:
	if (metadata tag "914item_select" of player) exists:
		delete (metadata tag "914item_select" of player)
	if (metadata tag "914item_select_id" of player) exists:
		delete (metadata tag "914item_select_id" of player)
		
on chat:
	if (metadata tag "914chance" of player) is true:
		if message parsed as int >= 1:
			if message parsed as int <= 100:
				SCP914_YMLValue((metadata tag "914chanceid" of player),"upgrades.%(metadata tag "914chanceup" of player)%.chance","%message parsed as int%")
				save yaml "item-%(metadata tag "914chanceid" of player)%"
				SCP914_ItemConfig(player,(metadata tag "914chanceid" of player))
		cancel event
		delete (metadata tag "914chance" of player)
		delete (metadata tag "914chanceid" of player)
		delete (metadata tag "914chanceup" of player)
		
on load:
	open {scp914::doors::*}
	set {_id} to 0
	loop {914items::amount} times:
		set {_id} to loop-number
		load yaml "plugins/Skript/yaml/scp914items/item-%{_id}%" as "item-%{_id}%"
		loop all players:
			if loop-player is op:
				send "&7[&aSCP-914&7] | &fLoaded YAML item-%{_id}%" to loop-player
