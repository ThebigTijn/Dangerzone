on script load:
    load yaml "plugins/Skript/yaml/SCP/scp914.yml" as "scp914"

local function drop(item:item,amount:integer):
    drop {_amount} of {_item} at location(194.5, -35, 41.5, world "world") without velocity

function scp914ItemAdd(mode:string,input:item,output:item):
    set yaml value "%{_mode}%.%1 of {_input}%" from "scp914" to {_output}
    save yaml "scp914"

function scp914ItemDelete(mode:string,input:item):
    delete yaml value "%{_mode}%.%1 of {_input}%" from "scp914"
    save yaml "scp914"

function scp914ItemGet(mode:string,input:item) :: string:
    return "&c&lERROR" if yaml value "%{_mode}%.%1 of {_input}%" from "scp914" is not set
    set {_output} to yaml value "%{_mode}%.%1 of {_input}%" from "scp914"
    return "%{_output}%"


local function scp914Start(): #Starting Machine
    set {_location} to block at location(190.5, -34.5, 40.5, world "world")
    set {_itementity} to a random element of dropped items in radius 2 around {_location} #needed to kill dropped item
    set {_input} to item of {_itementity} #needed to be parsed as item
    {_input} is set
    play sound "minecraft:entity.minecart.riding" with volume 10 at location(194.5, -35, 41, world "world")
    open block at location(194, -35, 41, world "world")
    open block at location(190, -35, 41, world "world")
    kill {_itementity}
    scp914Mode({_input})

local function scp914Mode(input:item): #Detecting the Mode and if we need to run
    set {_mode} to uncolored line 3 of block at location(192, -35, 42, world "world")
    if {_mode} = "ROUGH":
        set {_scrap} to raw copper named "&7&lPile of Scraps" with lore "&8&oA heap of worthless fragments" and "&8&owonder what this was before..."
        drop({_scrap},item amount of {_input})
        stop
    if scp914ItemGet({_mode},{_input}) contains "&c&lERROR":
        drop({_input}, item amount of {_input})
        stop
    scp914Run({_mode},{_input})

local function scp914Run(mode:string,input:item): #Run machine
    yaml value "%{_mode}%.%1 of {_input}%" from "scp914" is set
    set {_output} to yaml value "%{_mode}%.%1 of {_input}%" from "scp914"
    drop({_output},item amount of {_output}*item amount of {_input})

local function scp914Selection(p:player):
    create a gui with virtual hopper inventory named "           &8SCP-914 &a(Safe)":
        format gui slot 0 with black stained glass pane named "&8ROUGH":
            cancel event
            set line 3 of block at location(192, -35, 42, world "world") to "&8ROUGH"
            close {_p}'s inventory
        format gui slot 1 with red stained glass pane named "&cCOARSE":
            cancel event
            set line 3 of block at location(192, -35, 42, world "world") to "&cCOARSE"
            close {_p}'s inventory
        format gui slot 2 with white stained glass pane named "&f1:1":
            cancel event
            set line 3 of block at location(192, -35, 42, world "world") to "&f1:1"
            close {_p}'s inventory
        format gui slot 3 with lime stained glass pane named "&aFINE":
            cancel event
            set line 3 of block at location(192, -35, 42, world "world") to "&aFINE"
            close {_p}'s inventory
        format gui slot 4 with green stained glass pane named "&2VERY FINE":
            cancel event
            set line 3 of block at location(192, -35, 42, world "world") to "&2VERY FINE"
            close {_p}'s inventory
        open gui to {_p}


on right click on lever:
    if block behind event-block is blackstone:
        scp914Start()

on right click on dark oak sign:
    if block behind event-block is blackstone:
        cancel event
        scp914Selection(player)