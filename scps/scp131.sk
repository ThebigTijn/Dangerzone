#I set 131's movement speed to 0 instead of setting its AI off because if its AI is off then it cant
#be knocked back and it cant turn its head using with skript

function scp131(l: location):
    scp131spawn({_l}, "&6&lSCP-131-A &r&a(SAFE)")
    scp131spawn({_l}, "&c&lSCP-131-B &r&a(SAFE)")

local function scp131spawn(l: location, n: text):
    spawn slime at {_l}:
        add nbt from "{Size:0,PersistenceRequired:1,Silent:1b,Tags:[""131""]}" to nbt of entity
        set name of entity to {_n}
        apply resistance 5 without particles to entity for 1 year
        set {_e} to entity

    while {_e} is alive: #its better then every 0.25 seconds because it only works when 131 is alive
        wait 1 seconds
        delete {_b}
        if distance between {_e} and {scp::173} <= 15:
            set {_e}'s movement speed attribute to 0
            force {_e} to face {scp::173} at head rotation speed 99999
        else:
            reset {_e}'s movement speed attribute

on entity target:
    event-entity is a slime
    event-entity != {scp::999}
    cancel event

on entity jump:
    event-entity is a slime
    event-entity != {scp::999}
    event-entity's movement speed attribute is 0
    cancel event