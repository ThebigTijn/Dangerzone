local function getEquipment(p:player) :: item:
    set {_i} to brush
    set name of {_i} to "&7Mop"
    add hide enchants and hide attributes to {_i}
    return {_i}

function janitorMenu(p:player):
    play sound "block.note_block.harp" for {_p}
    create a gui with virtual hopper inventory named "&8Janitor":
        set {_head} to (player head with nbt from "{custom_name:'{""text"":""Button"",""color"":""gold"",""underlined"":true,""bold"":true,""italic"":false}',profile:{id:[I;2102559070,536168343,-1912831228,-907954843],properties:[{name:""textures"",value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvYmE4OTc4Y2NiZjU3NmY0NDZlMjFjNTFkM2U4MGZjN2Y4NTY2ZWI3MjY1Y2M0M2M0YWQ3MWNmYjc4YzE2NTI1NyJ9fX0=""}]}}")
        format gui slot (0 and 4) with black stained glass pane named " "
        format gui slot 1 with green dye named "&a&lINFORMATION" with lore "&8%font("system")%", "", "&7The custodian cleans spills to keep the place safe", "&7and tidy—you can help by cleaning up spills too!", ""
        format gui slot 2 with brush named "&b&lEQUIPMENT" with lore "&8%font("system")%", "", "&7The Custodian's Heroic Mop: Cleans spills and keeps things neat.", "&7Grab it and help maintain a tidy space!", "", "<##E5E4E2>&lCLICK HERE", "":
            {_p} has space for brush
            {_p} does not have brush
            receiveEquipment(player)
        format gui slot 3 with {_head} named "&c&lCLOSE":
            close inventory of {_p}
        open gui to {_p}

local function receiveEquipment(p:player):
    close inventory of {_p}
    send "<##E34234>&lSYSTEM &7» Received Equipment" to {_p}
    play sound "entity.item.pickup" for {_p}
    give {_p} getEquipment({_p})

local function getReward(p:player):
    set {_r} to round(random number between 0 and 1.5,2)
    currencyAdd({_p},"credits",{_r})
    send action bar "&e%font("cleaned stain")% &8[<##F5F5DC>+₵%{_r}%&8]" to {_p}
    play sound "entity.experience_orb.pickup" for {_p}

local function showDMG(p:player,b:block):
    set {_blockDMG} to updateBlock({_b})
    make all players see damage of {_b} as {_blockDMG}/10
    {_blockDMG} >= 10
    resetBlock({_b})
    createBlock({_b})
    getReward({_p})

local function updateBlock(b:block) :: int:
    set (metadata tag "janitorDMG" of {_b}) to (metadata tag "janitorDMG" of {_b})+1
    return (metadata tag "janitorDMG" of {_b})

local function resetBlock(b:block):
    make all players see damage of {_b} as 0
    delete (metadata tag "janitorDMG" of {_b})
    delete (string tag "custom;janitorDirt" of nbt of {_b})

local function createBlock(b:block):
    set block at {janitor.nextdirt} to brown carpet
    set (string tag "custom;janitorDirt" of nbt of {_b}) to "true"
    set {janitor.nextdirt} to {_b}
    set block within {_b} to air

on brush:
    (string tag "custom;janitorDirt" of nbt of event-block) = "true"
    showDMG(player,event-block)

on place:
    (string tag "custom;janitorDirt" of nbt of event-item) = "true"
    createBlock(event-block)

on break:
    (string tag "custom;janitorDirt" of nbt of event-block) = "true"
    player is op
    resetBlock(event-block)

on right click:
    event-entity is set
    name of event-entity = "Custodian@"
    janitorMenu(player)