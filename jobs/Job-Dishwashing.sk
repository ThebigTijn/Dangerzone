on load:
    load yaml "plugins/Skript/yaml/jobs/dishwashing.yml" as "dishwashing"

function getDishwashingItem(n: text) :: item:
    set {_name} to yaml value "%{_n}%.name" from "dishwashing"
    set {_item} to yaml value "%{_n}%.item" from "dishwashing"
    set {_i} to {_item} named {_name} with nbt compound from "{AttributeModifiers:[{AttributeName:""generic.attack_damage"",Name:""generic.attack_damage"",Amount:0,Operation:0,UUID:[I;549828122,1243172269,-1420982898,-1929562899]}]}" with all flags hidden
    set (int tag "IsDishes" of nbt compound from {_i}) to 1
    return {_i}

function dishwashingItemReceive(p: player):
    loop all items in {_p}'s inventory:
        (int tag "IsDishes" of nbt compound from loop-value) = 1 or 2
        add 1 to {_n}
        {_n} >= 4
        send "<##E34234>&lSYSTEM &7» Cannot carry anymore dishes" to {_p}
        stop
    set {_temp} to random element out of (yaml node keys "" from "dishwashing")
    give {_p} getDishwashingItem({_temp})
    play sound "item.armor.equip_generic" with volume 10 and pitch 1 to {_p}

function dishwashingEvent(p: player):
    (int tag "IsDishes" of nbt compound from {_p}'s tool) = 1
    set {_item} to {_p}'s held item
    remove {_item} from {_p}'s inventory
    loop 4 times:
        send action bar "&fWashing %name of {_item}%&f... &8[&c%textB({_t},3,"▉▉▉▉▉▉▉▉▉▉","&a","&7")%&8]" to {_p}
        play sound "block.water.ambient" at volume 0.5 for {_p}
        add 1 to {_t}
        wait a second
    set tag "IsDishes" of nbt compound from {_item} to 2
    give {_p} {_item} named "&f(Clean) %name of {_item}%"
    send action bar "&aSucessfully Washed &r%name of {_item}%" to {_p}

on right click:
    dishwashingItemReceive(player) if (metadata tag "Dishwashing_Receive_Block" of event-block) = true
    dishwashingEvent(player) if (metadata tag "Dishwashing_Sink_Block" of event-block) = true
    jobProductSell(player, "IsDishes", 2, "&fChef", 2, 4) if (metadata tag "Dishwashing_Sell_Block" of event-block) = true

on drop:
    remove event-item from player's inventory if tag "IsDishes" of nbt compound from event-item = 1 or 2