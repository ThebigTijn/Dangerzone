on load:
    load yaml "plugins/Skript/yaml/jobs/dishwashing.yml" as "dishwashing"

local function rData(data:string) :: object:
    return yaml value "%{_data}%" from "dishwashing"

local function getItem(s:string) :: item:
    set {_dish} to rData("%{_s}%.item")
    set {_name} to rData("%{_s}%.name")
    set {_i} to {_dish} named {_name} with nbt from "{AttributeModifiers:[{AttributeName:""generic.attack_damage"",Name:""generic.attack_damage"",Amount:0,Operation:0,UUID:[I;549828122,1243172269,-1420982898,-1929562899]}]}"
    set (int tag "dish" of nbt compound from {_i}) to 1
    add hide enchants and hide attributes to {_i}
    if {_s} != "Knife" or "Spoon":
        set lore of {_i} to "&0&k%unix timestamp of now%"
    return {_i}

local function receiveItem(p:player):
    loop all items in (inventory of {_p}) where [(int tag "dish" of nbt of input) = 1 or 2]:
        add 1 to {_n}
        {_n} >= 4
        send "<##E34234>&lSYSTEM &7Â» Cannot carry anymore dishes" to {_p}
        stop
    set {_temp} to random element out of (yaml node keys "" from "dishwashing")
    give {_p} getItem({_temp})
    play sound "item.armor.equip_generic" with volume 10 and pitch 1 to {_p}

local function washItem(p:player):
    set {_i} to (1 of tool of {_p})
    (int tag "dish" of nbt of {_i}) = 1
    remove {_i} from {_p}
    loop 4 times:
        send action bar "&fWashing %name of {_i}%&f... &8[&c%textB(10,loop-number,4)%&8]" to {_p}
        play sound "block.water.ambient" for {_p}
        wait a second
    set (int tag "dish" of nbt of {_i}) to 2
    set name of {_i} to "&f(Clean) &r%name of {_i}%"
    give {_p} {_i}
    send action bar "&aSucessfully Washed &r%name of {_i}%" to {_p}

local function sellDish(p:player):
    loop all items in (inventory of {_p}) where [(int tag "dish" of nbt of input = 2)]:
        add loop-value to {_items::*}
        add 1 to {_n}
    set {_r} to round(random number from 0 to 1,2)
    set {_total} to ({_n}*{_r})
    sellItem({_p},(tool of {_p}),{_total})
    remove {_items::*} from {_p}

on right click:
    if name of event-entity = "Dishwasher@":
        receiveItem(player)
    else if event-block is set:
        if (string tag "custom;dw-sink" of nbt of event-block) = "true":
            cancel event
            (int tag "dish" of nbt of tool of player) = 1
            washItem(player)
        else if (string tag "custom;dw-shelf" of nbt of event-block) = "true":
            (int tag "dish" of nbt of tool of player) = 2
            sellDish(player)
        
on drop:
    (int tag "dish" of nbt of event-item) = 1 or 2
    remove event-item from player