on load:
    load yaml "plugins/Skript/yaml/jobs/laundromat.yml" as "laundromat"

function getLaundromatItem(n: text) :: item:
    set {_name} to yaml value "%{_n}%.name" from "laundromat"
    set {_item} to yaml value "%{_n}%.item" from "laundromat"
    set {_i} to {_item} named {_name} with nbt compound from "{AttributeModifiers:[{AttributeName:""generic.armor_toughness"",Name:""generic.armor_toughness"",Amount:0,Operation:0,UUID:[I;549828122,1243172269,-1420982898,-1929562899]}]}" with all flags hidden
    set tag "custom;IsLaundry" of nbt compound from {_i} to true
    return {_i}

function laundromatReceiveItem(p: player):
    loop all items in {_p}'s inventory:
        tag "custom;IsLaundry" of nbt compound from loop-value = true
        add 1 to {_n}
        {_n} >= 4
        send "<##E34234>&lSYSTEM &7» You cannot carry more clothes" to {_p}
        stop
    set {_temp} to random element out of (yaml node keys "" from "laundromat")
    give {_p} getLaundromatItem({_temp})
    play sound "item.armor.equip_generic" with volume 10 and pitch 1 to {_p}

function laundromatMachine(p: player):
    set {_u} to uuid of {_p}
    metadata tag "Laundromat_Power" of {_p} ? false = false
    set metadata tag "Laundromat_Machine" of {_p} to virtual brewing stand inventory named "&8%font("washing machine")%"
    set slot 0 and 2 of metadata tag "Laundromat_Machine" of {_p} to item frame named " "
    set slot 4 of metadata tag "Laundromat_Machine" of {_p} to blaze powder named "&7Power"
    open metadata tag "Laundromat_Machine" of {_p} to {_p}

function laundromatMachineEvent(p: player, action: text, name: text, sound: text):
    set {_u} to uuid of {_p}
    #CHECK IF ALREADY ACTIVATED
    metadata tag "Laundromat_Power" of {_p} ? false = false
    close {_p}'s inventory
    set metadata tag "Laundromat_Power" of {_p} to true
    #WASHING AND DRYING EVENT
    loop 5 times:
        send action bar "&7%{_action}%... &8[&c%textB({_integer} + 1,5,"▉▉▉▉▉▉▉▉▉▉","&a","&7")%&8]" to {_p}
        play sound "%{_sound}%" at volume 0.5 for {_p}
        add 1 to {_integer}
        wait a second
    send action bar "&aTask Complete" to {_p}
    play sound "block.composter.ready" at volume 0.5 for {_p}
    open metadata tag "Laundromat_Machine" of {_p} to {_p}
    #WHEN INVENTORY OPENS, MOVES ITEM FROM SLOT TO ANOTHER
    set slot 3 of metadata tag "Laundromat_Machine" of {_p} to air
    set slot 1 of metadata tag "Laundromat_Machine" of {_p} to (metadata tag "Laundromat_Item" of {_p}) named "&f(%{_name}%) &7%name of (metadata tag "Laundromat_Item" of {_p})%"
    set tag "custom;IsLaundry" of nbt compound from slot 1 of (metadata tag "Laundromat_Machine" of {_p}) to 2
    set metadata tag "Laundromat_Power" of {_p} to false

on inventory click:
    event-inventory = (metadata tag "Laundromat_Machine" of player)
    if index of event-slot = 4:
        #CHECKS TO SEE WHAT CYCLE WILL BE DONE (WASHING OR DRYING)
        laundromatMachineEvent(player, "Washing", "Washed", "block.water.ambient") if tag "custom;IsLaundry" of nbt compound from (slot 3 of metadata tag "Laundromat_Machine" of player) = 1
        laundromatMachineEvent(player, "Drying", "Dried", "block.composter.fill") if tag "custom;IsLaundry" of nbt compound from (slot 3 of metadata tag "Laundromat_Machine" of player) = 2
    #CHECK FOR IF PLAYER HAS ENOUGH SPACE
    if index of event-slot = 1 or 3:
        if player has enough space for event-item:
            give player event-item
            set slot 1 and 3 of metadata tag "Laundromat_Machine" of player to air
        else:
            send "<##E34234>&lSYSTEM &7» Inventory Full"

on inventory click:
    #BUNCH OF SPECIFIC CHECKS
    name of event-inventory = "&8%font("washing machine")%"
    cancel event
    tag "custom;IsLaundry" of nbt compound from event-item = 2
    slot 1 and 3 of metadata tag "Laundromat_Machine" of player is air
    #METADATA TAG IS SET TO CLICKED SLOT AND PLACED INTO GUI
    set metadata tag "Laundromat_Item" of player to event-item
    set slot 3 of metadata tag "Laundromat_Machine" of player to event-item
    remove event-item from player's inventory

on right click:
    laundromatReceiveItem(player) if (metadata tag "Laundromat_Receive_Block" of event-block) = true
    laundromatMachine(player) if (metadata tag "Laundromat_Machine_Block" of event-block) = true
    jobProductSell(player, "custom;IsLaundry", 3, "&9Lander", 4, 6) if (metadata tag "Laundromat_Sell_Block" of event-block) = true