on load:
    load yaml "plugins/Skript/yaml/jobs/laundry.yml" as "laundry"

local function rData(data:string) :: object:
    return yaml value "%{_data}%" from "laundry"

local function getItem(s:string) :: item:
    set {_laundry} to rData("%{_s}%.item")
    set {_name} to rData("%{_s}%.name")
    set {_i} to {_laundry} named {_name}
    set (int tag "laundry" of nbt compound from {_i}) to 1
    add hide enchants and hide attributes to {_i}
    return {_i}

local function receiveItem(p:player):
    loop all items in inventory of {_p} where [(int tag "laundry" of nbt of input) = 1 or 2]:
        add 1 to {_n}
        {_n} >= 4
        send "<##E34234>&lSYSTEM &7» Cannot carry anymore laundry." to {_p}
        stop
    set {_temp} to random element out of (yaml node keys "" from "laundry")
    give {_p} getItem({_temp})
    play sound "item.armor.equip_generic" with volume 10 and pitch 1 to {_p}

local function laundryMachine(p:player):
    set {_g} to virtual hopper inventory named "&8%font("         laundry machine")%"
    set (metadata tag "laundry" of {_p}) to {_g}
    set slot 4 of {_g} to green stained glass pane named "&aPower"
    open {_g} to {_p}

local function washItem(p:player):
    set (metadata tag "laundryUsage" of {_p}) to true
    set {_inv} to (metadata tag "laundry" of {_p})
    loop 4 times:
        set {_slot} to (slot (loop-number - 1) of {_inv})
        {_slot} != air
        add {_slot} to {_list::*}
    close inventory of {_p}
    loop reversed 7 times:
        send action bar "&7%font("washing")%... &8[&f%loop-number%&8]" to {_p}
        wait a second
    loop {_list::*}:
        name of loop-value does not contain "(Clean)"
        set (name of loop-value) to "&f(Clean) &r%name of loop-value%"
        set (int tag "laundry" of nbt of loop-value) to 2
    give {_p} {_list::*}
    delete (metadata tag "laundryUsage" of {_p})

on inventory click:
    event-inventory = (metadata tag "laundry" of player)
    if index of event-slot = 4:
        cancel event
        loop 4 times:
            (slot (loop-number - 1) of event-inventory) != air
            washItem(player)
            exit loop

local function returnLaundry(p:player):
    remove (tool of {_p}) from {_p}
    set {_r} to round(random number from 0.5 to 1.5,2)
    currencyAdd({_p},"credits",{_r})
    set {_r} to round({_r}*1.5,2)
    currencyAdd({_p},"xp",{_r})
    send action bar "&a%font("returned laundry")% &8[<##F5F5DC>¢%{_r}%&8]" to {_p}

on right click:
    if event-entity is set:
        if (metadata tag "laundryUsage" of player) = true:
            send action bar "&c%font("you are already using a machine")%" to player
            stop
        name of event-entity = "Laundry@"
        receiveItem(player)
    event-block is set
    laundryMachine(player) if (string tag "custom;laundryMachine" of nbt of event-block) = "true"
    if (string tag "custom;laundryReturn" of nbt of event-block) = "true":
        (int tag "laundry" of nbt of (tool of player)) = 2
        cancel event
        returnLaundry(player)

command add-tool <string> <string>:
    permission: op
    trigger:
        set yaml value "%arg-1%.item" from "laundry" to (tool of player)
        set yaml value "%arg-1%.name" from "laundry" to arg-2
        save yaml "laundry"