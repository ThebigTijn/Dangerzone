on load:
    load yaml "plugins/Skript/yaml/jobs/laundry.yml" as "laundry"

function laundryGetItem(n: text) :: item:
    set {_name} to yaml value "%{_n}%.name" from "laundry"
    set {_item} to yaml value "%{_n}%.item" from "laundry"
    set {_i} to {_item} named {_name} with nbt compound from "{AttributeModifiers:[{AttributeName:""generic.armor_toughness"",Name:""generic.armor_toughness"",Amount:0,Operation:0,UUID:[I;549828122,1243172269,-1420982898,-1929562899]}]}" with all flags hidden
    set tag "custom;IsLaundry" of nbt compound from {_i} to true
    return {_i}

function laundryReceive(p: player):

    loop all items in {_p}'s inventory:
        tag "custom;IsLaundry" of nbt compound from loop-value = 1, 2, or 3
        add 1 to {_n}
        {_n} >= 4
        send "<##E34234>&lSYSTEM &7» You cannot carry more clothes" to {_p}
        stop

    set {_temp} to random element out of (yaml node keys "" from "laundry")
    give {_p} laundryGetItem({_temp})
    play sound "item.armor.equip_generic" with volume 10 and pitch 1 to {_p}

function laundryMachine(p: player):

    send "<##E34234>&lSYSTEM &7» Already using a machine" to {_p} if (metadata tag "Laundry_Power" of {_p}) is set
    (metadata tag "Laundry_Power" of {_p}) is not set

    set {_g} to virtual brewing stand inventory named "&8%font("washing machine")%"
    set (metadata tag "Laundry_Machine" of {_p}) to {_g}

    set slot (0 and 2) of {_g} to item frame named " "
    set slot 4 of {_g} to blaze powder named "&7Power"

    open {_g} to {_p}

function laundryEvent(p: player, s: string, sound: string, tag: int):

    close {_p}'s inventory
    set (metadata tag "Laundry_Power" of {_p}) to true

    loop 5 times:
        send action bar "&7Laundry Machine &8[&c%textB({_n} + 1,5,"▉▉▉▉▉▉▉▉▉▉","&a","&7")%&8]" to {_p}
        play sound {_sound} at volume 0.1 for {_p}
        add 1 to {_n}
        wait a second
    
    send action bar "&aTask Complete" to {_p}
    play sound "block.composter.ready" at volume 0.1 for {_p}

    set lore of (metadata tag "Laundry_Item" of {_p}) to "&f(%{_s}%)"
    set tag "custom;IsLaundry" of nbt compound from (metadata tag "Laundry_Item" of {_p}) to {_tag}

    set slot 3 of (metadata tag "Laundry_Machine" of {_p}) to air
    set slot 1 of (metadata tag "Laundry_Machine" of {_p}) to (metadata tag "Laundry_Item" of {_p})

    delete (metadata tag "Laundry_Power" of {_p})
    open (metadata tag "Laundry_Machine" of {_p}) to {_p}

on inventory click:

    name of event-inventory = "&8%font("washing machine")%"
    cancel event

    tag "custom;IsLaundry" of nbt compound from event-item = 1
    slot (1 and 3) of (metadata tag "Laundry_Machine" of player) = air

    set (metadata tag "Laundry_Item" of player) to event-item
    set slot 3 of (metadata tag "Laundry_Machine" of player) to event-item

    remove event-item from player's inventory

on inventory click:

    event-inventory = (metadata tag "Laundry_Machine" of player)
    cancel event

    if event-slot = 4:

        laundryEvent(player, "Washed", "block.water.ambient", 2) if tag "custom;IsLaundry" of nbt compound from (slot 3 of (metadata tag "Laundry_Machine" of player)) = 1
        laundryEvent(player, "Dried", "block.composter.fill", 3) if tag "custom;IsLaundry" of nbt compound from (slot 3 of (metadata tag "Laundry_Machine" of player)) = 2

    if event-slot = 1 or 3:

        send "<##E34234>&lSYSTEM &7» Inventory Full" to player if player doesn't have enough space for event-item
        player has enough space for event-item

        set slot (1 and 3) of (metadata tag "Laundry_Machine" of player) to air
        give player event-item

on right click:
    laundryReceive(player) if (metadata tag "Laundry_Receive" of event-block) = true
    laundryMachine(player) if (metadata tag "Laundry_Machine" of event-block) = true
    jobProductSell(player, "custom;IsLaundry", 3, "&9Lander", 4, 6) if (metadata tag "Laundry_Sell" of event-block) = true
