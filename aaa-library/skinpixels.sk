import:
    java.net.URL
    javax.imageio.ImageIO
    net.md_5.bungee.api.ChatColor
    java.awt.Color

function return_Character(character: string, loopAmount: number) :: string:
    set {_returnString} to ""
    loop {_loopAmount} times:
        set {_returnString} to "%{_returnString}%%{_character}%"
    return {_returnString}

function join_Msg(plr: player):
    loop 250 times:
        send " " to {_plr}
    run section {-pixel} async with {_plr} and store result in {_s::*} and wait
    set {_pixels::*} to {_s::*}
    set {_strings::*} to (" &f&lWELCOME TO &c&lDANGERZONE", " &7Secure. Contain. Protect.", "" , " &f%font("ip:")% &b%font("dangerzone.minehut.gg")%", " &f%font("store:")% &e%font("store.dangerzone.com")% ", " ", " &7&oEnjoy your stay!" , "")
    send "&8&m%return_Character(" " , 60)%" to {_plr}
    loop {_pixels::*}:
        send "%loop-value% %{_strings::%floor(loop-index parsed as a number)%} ? ""%" to {_plr}
    send "&8&m%return_Character(" " , 60)%" to {_plr}

on load:
    create section with {_player} stored in {-pixel}:
        set {_player} to "%{_player}%" parsed as a offline player
        return {skin::%{_player}%::*} if {skin::%{_player}%::*} is set
        set {_} to ""
        set {_image} to ImageIO.read(new URL("https://mc-heads.net/avatar/%uuid of {_player}%/8"))
        loop 8 times:
            loop 8 times:
                set {_line} to ((loop-value-1) - 1)
                set {_skin::%{_line}%} to join {_skin::%{_line}%} and "%ChatColor.of(new Color({_image}.getRGB((loop-value-2) - 1, {_line})))%â–ˆ"
        set {skin::%{_player}%::*} to {_skin::*}
        return {_skin::*}